//created on: Jun 8, 2018
package intensiveCare

import drools.model.reports.HeartBeatEvent;
import drools.model.reports.DialysisEvent;
import drools.model.reports.OxygenEvent;
import drools.model.Chart;
import drools.model.IntensiveCareReport;
import drools.model.Examination;
import drools.model.Disease;
import java.util.Date;

rule "Oxygen Level Alert"
	no-loop
	agenda-group "alerts"
    when
    	$ol1: OxygenEvent($lvl: oxygenBloodLevel, oxygenBloodLevel < 70, chart != null, $chart: chart)
    	$icReport: IntensiveCareReport(chart != null, chart == $chart)
    	Number(intValue == 0) from accumulate (
    		$ol2: OxygenEvent(
    			this != $ol1,
				chart != null,
    			chart == $chart,
    			oxygenBloodLevel < $lvl,
    			this meets[15m] $ol1
    		),
    		count(1)
    	)
    then
        System.out.println("Patinent's oxygen blood level has dropped below 70mmHg and is still dropping -> "+$chart.getPatient());
        $icReport.setTimeStamp(new Date());
       	$icReport.setOxygenOk(false);
       	update($icReport);
end

rule "Tachycardia"
	no-loop
	agenda-group "alerts"
    when
    	$hb1: HeartBeatEvent(chart != null,$chart: chart)
    	$icReport: IntensiveCareReport(chart != null,chart == $chart)
        Number(intValue >= 25) from accumulate(
        	$hb2: HeartBeatEvent(
        		this != $hb1,
				chart != null,
        		chart == $chart,
        		this meets[10s] $hb2
        	),
        	count(1)
        )
    then
        System.out.println("There were more that 25 hbts in less than 10s -> "+$chart.getPatient());
        $icReport.setTimeStamp(new Date());
       	$icReport.setHeartOk(false);
       	update($icReport);
end

rule "Urgent  dialysis"
	no-loop
	agenda-group "alerts"
    when
		$dl1: DialysisEvent(chart != null, $chart: chart)
    	Number(doubleValue < 100) from accumulate (
    		$dl2: DialysisEvent(
    			this != $dl1,
				chart != null,
    			chart == $chart,
    			this meets [12h] $dl1,
    			$q: urineQuantity
    		),
    		sum($q)
    	)
        Number(intValue >= 10) from accumulate(
    		$hb1: HeartBeatEvent(chart != null,chart == $chart) and
        	$hb2: HeartBeatEvent(
        		this != $hb1,
				chart != null,
        		chart == $chart,
        		this meets[10s] $hb1
        	),
        	count(1)
        )
        $icReport: IntensiveCareReport(chart != null, chart == $chart)
        Number(intValue > 0) from accumulate(
        	Chart(examinations != null, $exams: examinations) from $chart and
        	Examination(disease != null, $diag: disease) from $exams and
        	Disease(name == "Hronicna bubrezna bolest") from $diag,
        	count(1)
        )
    then
        System.out.println("Patient " + $chart.getPatient() + " needs dialysis!");
        $icReport.setTimeStamp(new Date());
       	$icReport.setUrinOk(false);
       	update($icReport);
end
