//created on: Jun 4, 2018
package thirdGroup

//list any import classes here.
import drools.model.Chart;
import drools.model.Examination;
import drools.model.Symptom;
import drools.model.Disease;
import java.util.Date;
import drools.model.Prescription;
import drools.model.enums.DrugType;
import drools.model.Drug;

//declare any global variables here

rule "Hronicna bubrezna bolest"
	salience $count.intValue()*10000/7+$diabetes.intValue()*10000/7+$hyperTens.intValue()*10000/7
	//activation-group "x"
	agenda-group "diagnose"
    when
        Chart($exams: examinations)
    	$exam: Examination($symptoms: symptoms)
    	$count: Number(intValue > 1) from accumulate (
    		Symptom(name=="Zamor" || name=="Nocturia" || 
    			name=="Otoci nogu i zglobova" || name=="Gusenje" || 
    			name=="Bol u grudima") from $symptoms,
    		count(1)
    	)
    	$diabetes: Number(intValue > -1) from accumulate (
			Examination($dis: disease, $dis != null) from $exams and
			Disease(name == "Dijabetes") from $dis,
    		count(1)
    	)
    	$hyperTens: Number(intValue > -1) from accumulate (
        	Examination(
        		this != $exam,
        		$diag: disease, 
        		disease != null,
        		this meets[180d] $exam) from $exams and
        	Disease(name == "Hipertenzija") from $diag,
        	count(1)
    	)
    	eval($diabetes.intValue()+$hyperTens.intValue() > 0)
    then
    	System.out.println("Salience je "+($count.intValue()*10000/7+$diabetes.intValue()*10000/7+$hyperTens.intValue()*10000/7));
    	System.out.println("Count: "+$count.intValue()+", diabetes: "+$diabetes.intValue()+", hyperTens: "+$hyperTens.intValue());
        Disease d = new Disease(6, "Hronicna bubrezna bolest", null);
    	insert(d);
        System.out.println("U pitanju je hronicna bubrezna bolest");

end

rule "Akutna bubrezna povreda"
    //include attributes such as "salience" here...
    salience ($count.intValue()*10000/8+$postOp.intValue()*10000/8+$highTemp.intValue()*10000/8+$antiBio.intValue()*10000/8)
    //activation-group "x"
    agenda-group "diagnose"
    when
        Chart($exams: examinations)
    	$exam: Examination($symptoms: symptoms)
    	$count: Number(intValue > 1) from accumulate (
    		Symptom(name=="Zamor" || name=="Dijareja" || 
    			name=="Otoci nogu i zglobova" || name=="Gusenje" || 
    			name=="Bol u grudima") from $symptoms,
    		count(1)
    	)
    	$postOp: Number(intValue > -1) from accumulate (
    		Symptom(name=="Oporavlja se od operacije") from $symptoms,
    		count(1)
    	)
    	$highTemp: Number(intValue > -1) from accumulate (
    		Examination(
        		this != $exam,
        		$symptoms1: symptoms,
        		this meets[14d] $exam) from $exams and
        	Symptom(name == "Temperatura veca od 38" || name == "Temperatura od 40 do 41") from $symptoms1,
        	count(1)
    	)
    	$antiBio: Number(intValue > -1) from accumulate (
    		Examination(
        		this != $exam,
        		$prescr: prescription,
        		prescription != null,
        		this meets[14d] $exam) from $exams and
        	Prescription($drug: drug) from $prescr and
        	Drug(drugType == DrugType.ANTIBIOTIC) from $drug,
        	count(1)
    	)
    	eval($postOp.intValue() + $highTemp.intValue() + $antiBio.intValue() > 0)
    then
    	System.out.println("Salience je "+ ($count.intValue()*10000/8+$postOp.intValue()*10000/8+$highTemp.intValue()*10000/8+$antiBio.intValue()*10000/8));
    	System.out.println("Count: "+$count.intValue()+", postOp: "+$postOp.intValue()+", highTemp: "+$highTemp.intValue()+", antiBio: "+$antiBio.intValue()); 
        Disease d = new Disease(7, "Akutna bubrezna povreda", null);
    	insert(d);
        System.out.println("U pitanju je akutna bubrezna povreda");

end
