//created on: Jun 6, 2018
package reports

import java.util.Date;
import drools.model.Chart;
import drools.model.Examination;
import drools.model.Disease;
import drools.model.Prescription;
import drools.model.enums.DrugType;
import drools.model.Drug;

rule "Hronicna oboljenja"
	agenda-group "reports"
    when
        Chart($exams: examinations, $patient: patient)
        $c1: Number(intValue > 5) from accumulate(
	        $ex: Examination($disease: disease) from $exams and
	        Disease(name!= "Prehlada", name!= "Groznica", $dName: name) from $disease and
        	Examination($dis: disease, this!= $ex, this meets[730d] $ex) from $exams and
        	Disease(name == $dName) from $dis,
	        count(1)
	    )
    then
        System.out.println($patient+" moguce boluje od hronicne bolesti "+$c1.intValue());
end

rule "Zavisnici"
	agenda-group "reports"
    when
        Chart($exams: examinations, $patient: patient)
        Number(intValue > 5) from accumulate(
	        $ex: Examination() from $exams and
	        Examination(this != $ex, this meets[180d] $ex, $pres1: prescription) from $exams and
	        Prescription($drug1: drug) from $pres1 and
	        Drug(drugType == DrugType.ANALGESIC) from $drug1,
	        count(1)
	    )	
    then
        System.out.println($patient+" je moguci zavisnik");

end

rule "Oslabljen imunitet"
    agenda-group "reports"
    when
        Chart($exams: examinations, $patient: patient)
        $analgDis: Number(intValue >= 10) from accumulate (
        	$ex: Examination() from $exams and
        	Examination(this != $ex, this meets[365d] $ex, $pres: prescription) from $exams and
        	Prescription($drug: drug) from $pres and
        	Drug(drugType == DrugType.ANALGESIC) from $drug,
        	count(1)
        )
        $allDis: Number(intValue >= 0) from accumulate (
        	$ex: Examination() from $exams and
        	Examination(this != $ex, this meets[365d] $ex) from $exams,
        	count(1)
        )
        eval($analgDis == $allDis)
    then
        System.out.println($patient+" moguce ima oslabljen imunitet");

end


