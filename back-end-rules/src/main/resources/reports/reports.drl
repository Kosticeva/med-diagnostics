//created on: Jun 6, 2018
package reports

import java.util.Date;
import drools.model.Chart;
import drools.model.Examination;
import drools.model.Disease;
import drools.model.Prescription;
import drools.model.enums.DrugType;
import drools.model.Drug;
import drools.model.Patient;
import java.util.List;
import drools.model.Doctor;

global List<Patient> chronics;
global List<Patient> addicts;
global List<Patient> weaks;

rule "Hronicna oboljenja"
	agenda-group "reports-chronics"
    when
        Chart( $exams: examinations, $patient: patient)
        $maxDate: Number() from accumulate (
        	Examination($date: date, $dis: disease) from $exams and
        	Disease(name != "Prehlada", name != "Groznica") from $dis and
        	Date($time: time) from $date,
        	max($time)
        )
        $ex: Examination($date: date, $di: disease) from $exams
        Date(time == $maxDate) from $date
        Disease($dName: name) from $di
        $c: Number(intValue > 4) from accumulate(
       		Examination($ds: disease, this!= $ex, this meets[730d] $ex) from $exams and
        	Disease(name == $dName) from $ds,
	        count(1)
        )
    then
        System.out.println($patient.getFirstName()+" moguce boluje od hronicne bolesti ("+$c+") "+$ex.getId()+", "+$ex);
        chronics.add($patient);
end

rule "Zavisnici"
	agenda-group "reports-addicts"
    when
        Chart($exams: examinations, $patient: patient)
        $maxDate: Number() from accumulate (
        	Examination($date: date) from $exams and
        	Date($time: time) from $date,
        	max($time)
        )
        $ex: Examination($date: date) from $exams
        Date(time == $maxDate) from $date
        $c: List(size > 5) from accumulate(
	       	Examination(this != $ex, this meets[180d] $ex, $pres1: prescription, $dd: doctor) from $exams and
	        Prescription($drug1: drug) from $pres1 and
	        Drug(drugType == DrugType.ANALGESIC) from $drug1,
	        collectList($dd)
	    )
	    Number(intValue > 0) from accumulate (
        	$d1: Doctor() from $c and
        	$d2: Doctor(this != $d1) from $c and
        	$d3: Doctor(this != $d1, this != $d2) from $c,
        	count(1)
        )
    then
        addicts.add($patient);
end

rule "Oslabljen imunitet"
    agenda-group "reports-weaks"
    when
        Chart($exams: examinations, $patient: patient)
        $maxDate: Number() from accumulate (
        	Examination(date != null, $date: date) from $exams and
        	Date($time: time) from $date,
        	max($time)
        )
        $ex: Examination($date: date) from $exams
        Date(time == $maxDate) from $date
        $analgDis: List(size >= 10) from accumulate (
        	Examination(this != $ex, this meets[365d] $ex, $pres: prescription, $dis: disease) from $exams and
        	Prescription($drug: drug) from $pres and
        	Drug(drugType == DrugType.ANTIBIOTIC) from $drug,
        	collectList($dis)
        )
        $allDis: List(size >= 10) from accumulate (
        	Examination(this != $ex, this meets[365d] $ex, $dis: disease) from $exams,
        	collectList($dis)
        )
        Number(intValue > 0) from accumulate (
        	$d1: Disease() from $analgDis and
        	$d2: Disease(this != $d1) from $analgDis,
        	count(1)
        )
        eval($analgDis.size() == $allDis.size())
    then
        System.out.println($patient.getFirstName()+" moguce ima oslabljen imunitet ("+$analgDis+" vs "+$allDis+")");
		weaks.add($patient);
end


